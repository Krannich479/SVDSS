### MAPPING TO HG CONTIGS ###
rule bbmap_align_qcontigs:
    input:
        index = f"{qcontigs}-bbmapindex",
        fq = pjoin(outdir, "pp", "{over}", "sspecific.short.fq")
    output:
        sam = pjoin(outdir, "pp", "{over}", "sspecific.short.hg-contigs.sam")
    threads: nths
    shell:
        """
        bbmap.sh threads={threads} k=11 usequality=f ambiguous=all path={input.index} in={input.fq} outm={output.sam}
        """

rule minimap2_qcontigs:
    input:
        fa = qcontigs,
        fq = pjoin(outdir, "pp", "{over}", "sspecific.long.fq")
    output:
        sam = pjoin(outdir, "pp", "{over}", "sspecific.long.hg-contigs.sam")
    threads: nths
    shell:
        """
        minimap2 -t {threads} --split-prefix 1 -ax asm20 {input.fa} {input.fq} > {output.sam}
        """

### MAPPING TO NA CONTIGS ###
rule bbmap_align_rcontigs:
    input:
        index = f"{rcontigs}-bbmapindex",
        fq = pjoin(outdir, "pp", "{over}", "sspecific.short.fq")
    output:
        sam = pjoin(outdir, "pp", "{over}", "sspecific.short.na-contigs.sam")
    threads: nths
    shell:
        """
        bbmap.sh threads={threads} k=11 usequality=f ambiguous=all path={input.index} in={input.fq} outm={output.sam}
        """

rule minimap2_rcontigs:
    input:
        fa = rcontigs,
        fq = pjoin(outdir, "pp", "{over}", "sspecific.long.fq")
    output:
        sam = pjoin(outdir, "pp", "{over}", "sspecific.long.na-contigs.sam")
    threads: nths
    shell:
        """
        minimap2 -t {threads} --split-prefix 1 -ax asm20 {input.fa} {input.fq} > {output.sam}
        """

rule clean_bbmap_1:
    input:
        sam = pjoin(outdir, "pp", "{over}", "sspecific.short.{sample}-contigs.sam")
    output:
        sam = pjoin(outdir, "pp", "{over}", "sspecific.short.{sample}-contigs.cleared.sam")
    shell:
        """
        python3 {sdir}/clean_bbmap.py {input.sam} > {output.sam}
        """

### ANALYSIS ###
rule cprecision:
    input:
        sam1 = pjoin(outdir, "pp", "{over}", "sspecific.short.hg-contigs.cleared.sam"),
        sam2 = pjoin(outdir, "pp", "{over}", "sspecific.long.hg-contigs.sam"),
        sam3 = pjoin(outdir, "pp", "{over}", "sspecific.short.na-contigs.cleared.sam"),
        sam4 = pjoin(outdir, "pp", "{over}", "sspecific.long.na-contigs.sam"),
        fq = pjoin(outdir, "pp", "{over}", "sspecific.fq")
    output:
        txt = pjoin(outdir, "pp", "{over}", "Cprecision.txt")
    threads: 1
    shell:
        """
        python3 {sdir}/analyze.py pre1 {input.sam1} {input.sam2} {input.sam3} {input.sam4} {input.fq} > {output.txt}
        """

rule plot_contigsqual:
    input:
        sam1 = pjoin(outdir, "pp", "{over}", "sspecific.short.hg-contigs.cleared.sam"),
        sam2 = pjoin(outdir, "pp", "{over}", "sspecific.long.hg-contigs.sam"),
        sam3 = pjoin(outdir, "pp", "{over}", "sspecific.short.na-contigs.cleared.sam"),
        sam4 = pjoin(outdir, "pp", "{over}", "sspecific.long.na-contigs.sam"),
        fq = pjoin(outdir, "pp", "{over}", "sspecific.fq")
    output:
        png = pjoin(outdir, "pp", "{over}", "Cplot.png")
    threads: 1
    shell:
        """
        python3 {sdir}/analyze.py cplot {input.sam1} {input.sam2} {input.sam3} {input.sam4} {input.fq} {output.png}
        """
