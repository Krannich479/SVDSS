rule kmers_haplobbmap:
    input:
        index = pjoin(outdir, "HG00733.haps.fa-bbmapindex"),
        fq = pjoin(outdir, "kmers", "{k}mers", "specific.fa")
    output:
        sam = pjoin(outdir, "kmers", "{k}mers", "specific.hg-haps.sam")
    threads: nths
    shell:
        """
        bbmap.sh threads={threads} k=11 usequality=f ambiguous=all path={input.index} in={input.fq} outm={output.sam}
        """

rule kmers_haplobwa:
    input:
        index = pjoin(outdir, "HG00733.haps.fa.amb"),
        fq = pjoin(outdir, "kmers", "kmers101", "specific.fa")
    output:
        sam = pjoin(outdir, "kmers", "kmers101", "specific.hg-haps.sam")
    params:
        index_prefix = pjoin(outdir, "HG00733.haps.fa")
    threads: nths
    shell:
        """
        bwa mem -t {threads} {params.index_prefix} {input.fq} > {output.sam}
        """

rule kmers_recintersect:
    input:
        bed1 = pjoin(outdir, "HGspecificvars_1.bed"),
        bed2 = pjoin(outdir, "HGspecificvars_2.bed"),
        albed = pjoin(outdir, "kmers", "{k}mers", "specific.hg-haps.sam.bed")
    output:
        bed1 = pjoin(outdir, "kmers", "{k}mers", "recintersect_1.bed"),
        bed2 = pjoin(outdir, "kmers", "{k}mers", "recintersect_2.bed")
    shell:
        """
        bedtools intersect -c -a {input.bed1} -b {input.albed} > {output.bed1}
        bedtools intersect -c -a {input.bed2} -b {input.albed} > {output.bed2}
        """

rule kmers_recall:
    input:
        bed1 = pjoin(outdir, "kmers", "{k}mers", "recintersect_1.bed"),
        bed2 = pjoin(outdir, "kmers", "{k}mers", "recintersect_2.bed")
    output:
        txt = pjoin(outdir, "kmers", "{k}mers", "recall.txt")
    shell:
        """
        python3 {sdir}/analyze.py rec {input.bed1} {input.bed2} 50 > {output.txt}
        """

rule kmers_preintersect:
    input:
        bed1 = pjoin(outdir, "HGallvars_1.bed"),
        bed2 = pjoin(outdir, "HGallvars_2.bed"),
        albed = pjoin(outdir, "kmers", "{k}mers", "specific.hg-haps.sam.bed")
    output:
        bed1 = pjoin(outdir, "kmers", "{k}mers", "preintersect_1.bed"),
        bed2 = pjoin(outdir, "kmers", "{k}mers", "preintersect_2.bed")
    shell:
        """
        bedtools intersect -wao -b {input.bed1} -a {input.albed} > {output.bed1}
        bedtools intersect -wao -b {input.bed2} -a {input.albed} > {output.bed2}
        """

rule kmers_precision:
    input:
        sample = pjoin(outdir, "kmers", "{k}mers", "specific.fa"),
        vcf = variants,
        bed1 = pjoin(outdir, "kmers", "{k}mers", "preintersect_1.bed"),
        bed2 = pjoin(outdir, "kmers", "{k}mers", "preintersect_2.bed"),
    output:
        txt = pjoin(outdir, "kmers", "{k}mers", "precision.txt")
    shell:
        """
        python3 {sdir}/analyze.py pre {input.sample} {input.vcf} {input.bed1} {input.bed2} > {output.txt}
        """
